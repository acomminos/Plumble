/*
 * Copyright (C) 2014 Andrew Comminos
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */





buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

dependencies {
    compile project(":libraries:Jumble")
    compile project(":libraries:libonionkit:libonionkit")
    compile 'com.android.support:support-v4:19.0.+'
    compile 'com.android.support:appcompat-v7:19.0.+'
    compile 'com.google.guava:guava:16.+'
}


// Keep my credentials private :^)
signingFile = file 'signing.gradle';
if(signingFile.exists()) apply from: 'signing.gradle'

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    // Fixes crash with support lib and gradle 0.9.1, see:
    // https://stackoverflow.com/questions/22509850/actionbaractivity-closes-after-pressing-hardware-menu-button-without-stack-trac
    aaptOptions.useAaptPngCruncher = true

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
        packageName "com.morlunk.mumbleclient"
        versionCode 61
        versionName "3.0.4"
        buildConfigField "boolean", "DONATE_NAG", "false"
    }

    productFlavors {

        official {
            packageName "com.morlunk.mumbleclient"
        }

        free {
            packageName "com.morlunk.mumbleclient.free"
            buildConfigField "boolean", "DONATE_NAG", "true"
        }

        jenkins {
            // Abuse Jenkins environment variables. Neat.
            env = System.getenv()
            if(env.containsKey("BUILD_NUMBER") && env.containsKey("BUILD_DISPLAY_NAME")) {
                versionCode Integer.parseInt(env.get("BUILD_NUMBER"))
                versionName env.get("BUILD_DISPLAY_NAME")
            } else {
                versionCode 0
                versionName "Unknown Nightly"
            }
            packageName "com.morlunk.mumbleclient.jenkins"
        }
    }

    lintOptions {
        abortOnError false
    }
}